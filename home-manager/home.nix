{
  config,
  pkgs,
  lib,
  osConfig,
  user,
  ...
}: let
  hasGui = osConfig.services.xserver.enable;
  hasWayland = osConfig.services.xserver.displayManager.gdm.wayland;
  hasGnome = osConfig.services.xserver.desktopManager.gnome.enable;
in {
  imports = [./modules/git ./modules/nushell];

  home = {
    username = user.name;
    homeDirectory =
      if pkgs.stdenv.isLinux
      then "/home/${config.home.username}"
      else "/Users/${config.home.username}";
    sessionVariables = {
      # XDG_CACHE_HOME = "$HOME/.cache";
      # XDG_CONFIG_HOME = "$HOME/.config";
      # XDG_DATA_HOME = "$HOME/.local/share";
      # XDG_STATE_HOME = "$HOME/.local/state";

      # Not officially in the specification
      XDG_BIN_HOME = "$HOME/.local/bin";
    };
    sessionPath = [config.home.sessionVariables."XDG_BIN_HOME"];
    packages = with pkgs; [enpass];
  };
  xdg.systemDirs.data = [
    # show desktop entries
    "$HOME/.nix-profile/share"
  ];

  # Add stuff for your user as you see fit:
  programs.neovim = {
    enable = true;
    defaultEditor = true;
    vimAlias = true;
    viAlias = true;
    extraLuaConfig = ''
      require("config.lazy")
    '';
    withNodeJs = true;
  };
  xdg.configFile."nvim" = {
    source = ./config/nvim;
    recursive = true;
  };
  #xdg.configFile."nvim/lazy-lock.json" = {
  #  enable = false;
  #  source = config.lib.file.mkOutOfStoreSymlink ../nvim/lazy-lock.json;

  #};

  xdg.configFile."nix/config.nix".text = ''
    { allowUnfree = true; }
  '';

  # Enable home-manager and git
  programs.home-manager.enable = true;

  programs.bash = {
    enable = true;
    initExtra = ''
      if [[ $(ps --no-header --pid=$PPID --format=comm) != "nu" && -z ''${BASH_EXECUTION_STRING} ]]; then
      	shopt -q login_shell && LOGIN_OPTION='--login' || LOGIN_OPTION='''
      	exec "${pkgs.nushell}/bin/nu" "$LOGIN_OPTION"
      fi
    '';
  };

  programs.rtx.enable = true;

  programs.zoxide = {
    enable = true;
    options = ["--cmd=j"];
  };

  programs.ripgrep.enable = true;
  programs.wezterm = {
    enable = hasGui;
    extraConfig =
      ''
        local config = wezterm.config_builder()
      ''
      + lib.optionalString hasWayland ''
        config.enable_wayland = true
      ''
      + ''
        return config
      '';
  };
  programs.starship = {
    enable = true;
    settings = {
      add_newline = true;
      continuation_prompt = "[:::](bright-black) ";
      character = {
        success_symbol = "[➜](bold green)";
        error_symbol = "[➜](bold blue)";
      };
      aws.disabled = true;
      directory.truncation_length = 5;
      php.format = "via [$symbol]($style)";

      python.format = "via [$${symbol}$${pyenv_prefix}($${version})]($style) ";
      status = {
        disabled = false;
        symbol = "✘";
      };
      shell = {
        disabled = false;
        nu_indicator = "";
        format = "([$indicator](style) )";
      };
      nix_shell = {
        impure_msg = "";
        # format = "via [$symbol($state)($name)]($style) ";
      };
    };
  };

  programs.firefox = {enable = hasGui;};

  programs.direnv = {
    enable = true;
    stdlib = ''
      ### Do not edit. This was autogenerated by 'rtx direnv' ###
      use_rtx() {
        direnv_load rtx direnv exec
      }
    '';
    nix-direnv.enable = true;
  };

  # home.sessionVariables."XCURSOR_THEME" = "Bibata-Modern-Classic";

  home.shellAliases = {
    "g" = "git";
    "ll" = "ls -l";
    "la" = "ls -la";
    "hm" = "home-manager";
  };

  programs.gpg.enable = true;
  services.gnome-keyring.enable = true;
  services.gpg-agent = {
    enable = true;
    enableSshSupport = true;
    sshKeys = ["3F9EFD3BDEA64344C9F1FF2B6230454F4BE7405F"];
    pinentryFlavor =
      if hasGnome
      then "gnome3"
      else "gtk2";
  };

  # Nicely reload system units when changing configs
  systemd.user.startServices = "sd-switch";

  dconf.settings = lib.mkIf hasGui {
    # "org/gnome/desktop/peripherals/touchpad/tap-to-click"
    "org/gnome/desktop/peripherals/touchpad" = {
      tap-to-click = true;
      two-finger-scrolling-enabled = true;
      # tapping = true;
      # scrollMethod = "natural";
      natural-scroll = false;
    };
    "org/gnome/shell" = {
      favorite-apps = ["org.gnome.Nautilus.desktop" "firefox.desktop" "discord.desktop" "org.wezfurlong.wezterm.desktop"];
    };
    "org/gnome/desktop/interface" = {
      text-scaling-factor = 1.25;
    };
  };

  home.stateVersion = "23.05";
}
